plugins {
  id "org.jetbrains.kotlin.multiplatform"
  id "com.android.library"
  id "convention.publication"
}

kotlin {
  android() {
    publishLibraryVariants("release")
  }
}

android {
  compileSdk = Consts.androidCompileSdk
  ndkVersion = Consts.androidNdk
  namespace = Consts.androidNS

  defaultConfig {
    minSdk = Consts.androidMinSdk
    targetSdk = Consts.androidTargetSdk

    ndkVersion = Consts.androidNdk
    ndk.abiFilters = Consts.ndkAbis

    sourceSets.main.java.srcDirs = [
        "src/main/java",
        "${rootProject.projectDir}/generated-src/java",
    ]

    externalNativeBuild {
      cmake {
        arguments '-DANDROID_STL=c++_shared',
            '-DNO_WEBSOCKET=ON', '-DNO_MEDIA=ON',
            '-DNO_EXAMPLES=ON', '-DNO_TESTS=ON'
        targets 'datachannel-static', 'datachannel_wrapper'
      }
    }

    consumerProguardFiles 'proguard-consumer-rules.pro'
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }

  packagingOptions {
    //exclude '**/libc++_shared.so'
    exclude '**/libcrypto.so'
    exclude '**/libssl.so'
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  externalNativeBuild {
    cmake {
      path file('../src/CMakeLists.txt')
      version '3.22.1'
    }
  }

  buildFeatures.prefab = true
}

dependencies {
  implementation(Kotlin.stdlib)
  implementation(AndroidX.annotation)

//  implementation "com.android.ndk.thirdparty:openssl:_"
}
